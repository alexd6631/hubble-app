plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id "org.jetbrains.kotlin.native.cocoapods"
}
repositories {
    google()
    jcenter()
    mavenCentral()
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId 'io.mkp.hubbleapp'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    buildFeatures {
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
    }
}

version = "1.0"

def coroutines_version = "1.3.2-1.3.60"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //implementation 'com.android.support:appcompat-v7:29.0.0'
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    def compose_version = "0.1.0-dev02"
    implementation "androidx.ui:ui-tooling:$compose_version"
    implementation "androidx.ui:ui-layout:$compose_version"
    implementation "androidx.ui:ui-material:$compose_version"

    def nav_version = "2.1.0"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "io.coil-kt:coil:0.9.2"

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
}

kotlin {
    android("android")
    // This is for iPhone emulator
    // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
    iosX64("ios") {
        binaries.debugFramework {
            freeCompilerArgs = freeCompilerArgs + ["-Xobjc-generics"]
        }

        binaries.releaseFramework {
            freeCompilerArgs = freeCompilerArgs + ["-Xobjc-generics"]
        }
    }

    iosArm64 {
        binaries.debugFramework {
            freeCompilerArgs = freeCompilerArgs + ["-Xobjc-generics"]
        }

        binaries.releaseFramework {
            freeCompilerArgs = freeCompilerArgs + ["-Xobjc-generics"]
        }
    }

    cocoapods {
        // Configure fields required by CocoaPods.
        summary = "Some description for a Kotlin/Native module"
        homepage = "Link to a Kotlin/Native module homepage"
    }

    sourceSets {
        core {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
            }
        }

        commonMain {
            dependsOn core
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"

                api project(":mvvm")
                implementation project(":hubblesite-client")
            }
        }
        commonTest {
            dependencies {
        		implementation kotlin('test-common')
        		implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
            dependencies {
                implementation kotlin('stdlib')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        iosMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutines_version"
            }


        }
        iosTest {
        }

        iosArm64Main {
            dependsOn iosMain
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
